/*
 * Copyright 2021 Benjamin Martin
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = 'net.lapismc'
archivesBaseName = "LapisChat"
version = '1.1.3'
description = "A spiritual successor to HeroChat, build from the ground up to be simple and extendable"
sourceCompatibility = '17'
targetCompatibility = '17'
defaultTasks 'clean', 'build', 'shadowJar'

repositories {
    mavenCentral()
    //Spigot Repo
    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/groups/public/')
    }
    //Personal Repo
    maven {
        name = "lapismc-repo"
        url = "https://maven.lapismc.net/repository/maven/"
    }
    //Vault Repo
    maven {
        url = uri('https://nexus.hc.to/content/repositories/pub_releases/')
    }
    //PlaceholderAPI Repo
    maven {
        url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/')
    }
    //Discord SRV
    maven { url 'https://nexus.scarsz.me/content/groups/public/' }
    maven { url 'https://m2.dv8tion.net/releases' }
}

dependencies {
    implementation 'net.lapismc:LapisCore:1.12.15'
    implementation 'net.lapismc:LapisUI:1.4.2'
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'com.discordsrv:discordsrv:1.27.0'
    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'
}

processResources {
    filter {
        it.replace('${project.version}', project.getVersion())
    }
}

shadowJar {
    archiveFileName.set(archivesBaseName + ".jar")
    exclude 'javax/**'
    relocate("net.lapismc.lapiscore", "net.lapismc.lapischat.utils.core")
    relocate("net.lapismc.lapisui", "net.lapismc.lapischat.utils.ui")
}

javadoc {
    source = sourceSets.main.allJava
    include("net/lapismc/lapischat/api/*.java")
    include("net/lapismc/lapischat/framework/*.java")
    include("net/lapismc/lapischat/events/*.java")
}


tasks.register('javadocJar', Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar, shadowJar
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name 'nexus'
            url "https://nexus.dnablue2112.com/repository/maven-releases/"
            credentials {
                username repoUser
                password repoPassword
            }
        }
    }
}